apply plugin: 'com.android.library'
apply from: "$rootDir/gradle-fury/gradle/android-support.gradle"

if (isReleaseBuild()) {
    status = 'release'
} else {
    status = 'integration'
}

android {

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    defaultConfig {

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        externalNativeBuild {
            cmake {
                arguments "-DANDROID_ARM_NEON=TRUE",
                        "-DANDROID_CPP_FEATURES=rtti exceptions",
                        "-DANDROID_TOOLCHAIN=clang",
                        "-DANDROID_PIE=ON",
                        "-DANDROID_STL=c++_shared",
                        "-DANDROID_PLATFORM=android-24",
                        "-DASSIMP_BUILD_ASSIMP_TOOLS=OFF",
                        "-DASSIMP_BUILD_TESTS=OFF",
                        "-DBUILD_SHARED_LIBS=YES",
                        "-DASSIMP_COVERALLS=NO"
                cFlags "-fexceptions", "-frtti", "-DASSIMP_BUILD_NO_OWN_ZLIB"
                cppFlags "-std=c++11 -fexceptions", "-frtti", "-DASSIMP_BUILD_NO_OWN_ZLIB",
                        "-DASSIMP_BUILD_NO_C4D_IMPORTER"
            }
        }

        ndk {
            // Specifies the ABI configurations of your native
            // libraries Gradle should build and package with your APK.
            abiFilters 'x86', 'x86_64', 'armeabi', 'armeabi-v7a',
                    'arm64-v8a'
        }
    }

    buildTypes {
        debug {
            testCoverageEnabled true
            minifyEnabled false
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        abortOnError false
    }

    externalNativeBuild {
        cmake {
            path 'CMakeLists.txt'
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile 'com.android.support:appcompat-v7:26.0.0-alpha1'
    testCompile 'junit:junit:4.12'
}

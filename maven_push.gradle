apply plugin: 'maven'
apply plugin: 'signing'

if (isReleaseBuild()) {
    println 'RELEASE BUILD'
} else {
    println 'DEBUG BUILD'
}

def getRepositoryUsername() {
    return System.getenv('SONATYPE_USERNAME') ?: hasProperty('SONATYPE_USERNAME') ? sonatype_user : ""
}

def getRepositoryPassword() {
    return System.getenv('SONATYPE_PASSWORD') ?: hasProperty('SONATYPE_PASSWORD') ? sonatype_pass : ""
}

String getBranchName() {
    return System.getenv('TRAVIS_BRANCH')
}

boolean isLocal() {
    return !isRelease() || (getRepositoryUsername().isEmpty() || getRepositoryPassword().isEmpty())
}

boolean isRelease() {
    return isTag() || getBranchName().equals("master")
}

afterEvaluate { project ->

    println "Is Tag ${isTag()}"
    println "Branch ${getBranchName()}"
    println "Is Release ${isReleaseBuild()}"
    println "Is Travis ${isTravis()}"
    println "Has Username ${!getRepositoryUsername().empty}"
    println "Has Password ${!getRepositoryPassword().empty}"
    println "Determined Version ${version}"

    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                pom.artifactId = POM_ARTIFACT_ID

                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                    authentication(userName: getRepositoryUsername(), password: getRepositoryPassword())
                }

                snapshotRepository(url: isTravis() ? "https://oss.sonatype.org/content/repositories/snapshots" : mavenLocal().url) {
                    authentication(userName: getRepositoryUsername(), password: getRepositoryPassword())
                }

                pom.project {
                    name POM_NAME
                    packaging POM_PACKAGING
                    description POM_DESCRIPTION
                    url POM_URL

                    scm {
                        url POM_SCM_URL
                        connection POM_SCM_CONNECTION
                        developerConnection POM_SCM_DEV_CONNECTION
                    }

                    licenses {
                        license {
                            name POM_LICENCE_NAME
                            url POM_LICENCE_URL
                            distribution POM_LICENCE_DIST
                        }
                    }

                    developers {
                        developer {
                            id POM_DEVELOPER_ID
                            name POM_DEVELOPER_NAME
                            email POM_DEVELOPER_EMAIL
                            organization POM_DEVELOPER_ORGANIZATION
                            organizationUrl POM_DEVELOPER_ORGANIZATION_URL
                        }
                    }
                }
            }
        }
    }

    signing {
        sign configurations.archives
    }

    task androidJavadocs(type: Javadoc) {
        source = android.sourceSets.main.java.srcDirs
        classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    }

    task androidJavadocsJar(type: Jar, dependsOn: androidJavadocs) {
        classifier = 'javadoc'
        from androidJavadocs.destinationDir
    }

    task androidSourcesJar(type: Jar) {
        classifier = 'sources'
        from android.sourceSets.main.java.sourceFiles
    }

    artifacts {
        archives androidSourcesJar
//        archives androidJavadocsJar
    }
}

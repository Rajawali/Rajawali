plugins {
    id 'com.android.library'
}

android {
    compileSdkVersion rootProject.compileSdk

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion rootProject.targetSdk
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        // VERSION_NAME no longer available as of 4.1
        // https://issuetracker.google.com/issues/158695880
        buildConfigField 'String', 'VERSION_NAME', "\"$version\""
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildTypes {
        debug {
            testCoverageEnabled true
            minifyEnabled false
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        abortOnError false
    }

    testOptions {
        animationsDisabled true
        unitTests {
            returnDefaultValues = true
            includeAndroidResources = true
        }
    }
}

dependencies {
    implementation 'androidx.annotation:annotation:1.2.0'
    testImplementation 'junit:junit:4.13.2'

    androidTestImplementation "androidx.test.espresso:espresso-core:$espresso_core"
    androidTestImplementation 'androidx.test:rules:1.3.0'
    androidTestImplementation "androidx.test:runner:1.3.0"
    androidTestImplementation "androidx.test:core:1.3.0"

    androidTestImplementation "androidx.test.ext:junit:1.1.2"
    androidTestImplementation "androidx.test.ext:truth:1.3.0"

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
}

apply from: '../publish.gradle'
apply plugin: 'kotlin-android'

task dokkaGhPages(type: org.jetbrains.dokka.gradle.DokkaTask) {
    outputFormat = 'html'
    outputDirectory = "$buildDir/gh-pages"
    sourceDirs = files(
            'src/main/java',
            '../vr/src/main/java',
            '../vuforia/src/main/java',
            '../wear/src/main/java',
    )
}
